buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:0.8"
    }
}


apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'shadow'

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.0'
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7'
    compile 'com.typesafe.akka:akka-actor_2.10:2.2.1'
    compile 'org.scala-lang:scala-library:2.10.2'
    compile 'net.sourceforge.jtds:jtds:1.2.7'
    compile 'com.timgroup:java-statsd-client:3.0.2'

    compile 'org.postgresql:postgresql:9.3-1102-jdbc41'
    compile 'ch.qos.logback:logback-classic:1.1.2'

    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'    
}

jar {
    manifest {
        attributes "Main-Class": "scorekeeper.ScoreKeeper"
        attributes "Built-Date": new Date() //now
        attributes "Built-By": System.getProperty("user.name")
        attributes "Build-Jdk": System.getProperty("java.version")
        attributes "Implementation-Title": project.name
        attributes "Implementation-Version": project.version
        attributes "Implementation-Vendor-Id": project.group
        [
                "BUILD_NUMBER": "Build-Number",
                "BUILD_ID"    : "Build-Id",
                "HG_COMMIT"   : "HG-Commit",
                "HG_BRANCH"   : "HG-Branch"
        ].each { envVar, attrName ->
            def envVal = System.getenv(envVar)
            if (envVal) {
                attributes([(attrName): envVal])
            }
        }
    }
}

assemble.dependsOn "shadowJar" // Include shadowed jar as part of standard project outputs
shadow {
    outputFile = file("${buildDir}/distributions/scorekeeper-${version}-shadow.jar")
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"

    transformer(com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer) {
        resource = "reference.conf"
    }
}

artifacts {
    archives(file("${buildDir}/distributions/scorekeeper-${version}-shadow.jar")) {
        name "scorekeeper"
        classifier "shadow"
        builtBy shadowJar
    }
}


